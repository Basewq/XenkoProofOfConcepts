shader ObjectInfoInputShader
{
    rgroup PerView.ObjectInfo
    {
        // Be sure to include <uint2> otherwise the shader will not correctly read the texture
        stage Texture2D<uint2> ObjectInfoData;    // As per shader ObjectInfoOutputShader, x stores the ModelComponentId, y stores the mesh & material indices.
    }

    uint GetModelComponentId(float2 textureLocation)
    {
        // Because this is a uint texture, we cannot use texture samplers.
        // It must use the Load method which requires the pixel location (ie. [0...TextureWidth/Height - 1]),
        // rather than UV location.

        uint modelCompId = ObjectInfoData.Load(int3(textureLocation, 0)).x;
        return modelCompId;
    }

    uint GetMeshIndex(float2 textureLocation)
    {
        uint meshIndexAndMaterialIndex = ObjectInfoData.Load(int3(textureLocation, 0)).y;
        return meshIndexAndMaterialIndex >> 16;     // The upper 16 bits
    }

    uint GetMaterialIndex(float2 textureLocation)
    {
        uint meshIndexAndMaterialIndex = ObjectInfoData.Load(int3(textureLocation, 0)).y;
        return meshIndexAndMaterialIndex & 0xFFFF;  // The lower 16 bits
    }
};
